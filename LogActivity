Sub LogActivity(sDatabasePath As String, sTableName As String, sActivityName As String, sComments As String, ParamArray Arguments())
'Arguments - firstly name of the field in database, later on its value

Dim cn As ADODB.Connection
Dim rs As ADODB.Recordset
Dim i As Long

If sDatabasePath = "" Then sDatabasePath = "X:\Excel\SAPscript.accdb"
If sTableName = "" Then sTableName = "MacroExecutions"

If Dir(sDatabasePath) = "" Then
    MsgBox "You have no access to database path!" & vbNewLine & "Call developer!" & vbNewLine & vbNewLine & "Macro will proceed but its activity will not be registered in db", vbInformation
    Exit Sub
End If



Set cn = New ADODB.Connection
Set rs = New ADODB.Recordset

cn.Open "Provider=Microsoft.ACE.OLEDB.12.0; Data Source=" & sDatabasePath & ";"
rs.Open sTableName, cn, adOpenDynamic, adLockOptimistic, adCmdTable

If (UBound(Arguments) - LBound(Arguments) + 1) Mod 2 <> 0 Then
    Debug.Print "Wrong (odd) number of arguments!!"
    Stop
End If

With rs
    .AddNew
    .Fields("UserName") = Application.UserName
    .Fields("SubName") = sActivityName
    .Fields("FileLocalization") = ThisWorkbook.Path
    .Fields("Comments") = sComments
    .Fields("MacroVersion") = ThisWorkbook.BuiltinDocumentProperties("Title")
    For i = LBound(Arguments) To UBound(Arguments) Step 2
        .Fields(Arguments(i)).Value = Arguments(i + 1)
    Next i
    .Update
End With

rs.Close
cn.Close

Set rs = Nothing
Set cn = Nothing

End Sub

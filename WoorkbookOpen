Function OpenWorkbook(Optional sFilePath As String, Optional sTitle As String, Optional sDefaultPath As String) As Workbook
'##################################################################################################################
'# Wojciech Dobrzycki                                                                                             #
'# 2016-05-05                                                                                                     #
'# Macro opens workbook basing on filepath or by manual selection, also chcecks if workbook is already opened     #
'##################################################################################################################

Dim sWkbName As String
Static sNewPath As String
Dim fso As Object
Set fso = CreateObject("scripting.filesystemobject")

If sDefaultPath = vbNullString And sNewPath = vbNullString Then
    sDefaultPath = ThisWorkbook.Path
ElseIf sNewPath <> vbNullString Then
    sDefaultPath = sNewPath
End If


If sFilePath <> vbNullString Then
    If Dir(sFilePath) <> "" Then
        sWkbName = fso.GetFileName(sFilePath)
        On Error Resume Next
        Set OpenWorkbook = Workbooks(sWkbName)
        Err.Clear
        On Error GoTo 0
        If OpenWorkbook Is Nothing = False Then GoTo WorkbookOpened
    Else
    'file does not exists
        MsgBox "Could not find file in directory:" & vbNewLine & sFilePath, vbCritical
        End
    End If
Else
    With Application.FileDialog(msoFileDialogOpen)
        .AllowMultiSelect = False
        .Title = sTitle
        .InitialFileName = sDefaultPath
        .Show
        If .SelectedItems.Count = 0 Then
            MsgBox "You did not choose any file!"
            End
        Else
        sFilePath = .SelectedItems.Item(1)
        sWkbName = fso.GetFileName(sFilePath)
        On Error Resume Next
        Set OpenWorkbook = Workbooks(sWkbName)
        Err.Clear
        On Error GoTo 0
        If OpenWorkbook Is Nothing = False Then GoTo WorkbookOpened
        End If
    End With
End If

Set OpenWorkbook = Workbooks.Open(Filename:=sFilePath)
sNewPath = OpenWorkbook.Path


WorkbookOpened:

Set fso = Nothing

End Function

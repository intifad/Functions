Function CreateDictionaryX(wks As Worksheet, StartRow As Long, EndRow As Long, bString As Boolean, ParamArray Cols()) As Dictionary

Dim sKey As String
Dim DictValue As clDictValues
Dim sinValue As Single
Dim SplitIndex As Long
Dim arrColKey()
Dim arrColVal()
Dim dictHelp As New Dictionary
Dim i As Long
Dim j As Long
Dim bKey As Boolean

'/Sizing arrays used to store columns numbers for keys and values
SplitIndex = WorksheetFunction.Match("x", Cols, 0) - 1 '-1 since function gives index as Base=1, but Base for an array is 0
ReDim arrColKey(SplitIndex - 1)
ReDim arrColVal(UBound(Cols) - SplitIndex - 1)
'//

'/Columns for keys
For i = LBound(Cols) To SplitIndex - 1
    arrColKey(i) = Cols(i)
Next i
'//

'/Columns for Values
j = LBound(arrColVal)
For i = SplitIndex + 1 To UBound(Cols)
    arrColVal(j) = Cols(i)
    j = j + 1
Next i
'//

'#Definign type of dictionary, does it contain strings or values
If bString Then
    '/Creating dictionary for values as Strings
    With wks
        For i = StartRow To EndRow
            Set DictValue = New clDictValues
            sKey = ""
            For j = LBound(arrColKey) To UBound(arrColKey)
                sKey = sKey & "***" & .Cells(i, arrColKey(j))
            Next j
            sKey = Mid(sKey, 4, Len(sKey))  'cuts first ***
               
            DictValue.sDictValue = ""
            For j = LBound(arrColVal) To UBound(arrColVal)
                DictValue.sDictValue = DictValue.sDictValue & "***" & .Cells(i, arrColVal(j))
            Next j
            
            DictValue.sDictValue = Mid(DictValue.sDictValue, 4, Len(DictValue.sDictValue))  'cuts first ***
            DictValue.NoRow = i
            If dictHelp.Exists(sKey) = False Then
                dictHelp.Add sKey, DictValue
                dictHelp(sKey).lDuplicates = 1
            Else
                dictHelp(sKey).lDuplicates = dictHelp(sKey).lDuplicates + 1
            End If
        Next i
    End With
    '//
Else
    '/Creating dictionary for values as longs (sums)
    With wks
        For i = StartRow To EndRow
        Set DictValue = New clDictValues
            Set DictValue = New clDictValues
            sKey = ""
            For j = LBound(arrColKey) To UBound(arrColKey)
                sKey = sKey & "***" & .Cells(i, arrColKey(j))
            Next j
            sKey = Mid(sKey, 4, Len(sKey)) 'cuts  first ****
               
            DictValue.dblDictValue = 0
            For j = LBound(arrColVal) To UBound(arrColVal)
                DictValue.dblDictValue = DictValue.dblDictValue + .Cells(i, arrColVal(j)).Value2
            Next j
            
            DictValue.NoRow = i
            If dictHelp.Exists(sKey) = False Then
                dictHelp.Add sKey, DictValue
                dictHelp(sKey).lDuplicates = 1
            Else
                dictHelp(sKey).dblDictValue = dictHelp(sKey).dblDictValue + DictValue.dblDictValue
                dictHelp(sKey).lDuplicates = dictHelp(sKey).lDuplicates + 1
            End If
        Next i
    End With
    '//
End If
'##

Set CreateDictionaryX = dictHelp

End Function
